import
  &StandardImport, &Constants
  &Components/Partials
  {} &Number

class Root extends FluxComponent
  @subscriptions :currentGame.currentGame

  @stateFields
    hoverNumber: null
    portrait: false

  ready: ({target}) ->
    @portrait = 1 > target.parent.currentSize.aspectRatio

  render: ->
    @ extract portrait
    @currentGame extract
      winner
      currentPlayerId
      players
      pawns

    Element
      draw: :white
      on: {} @ready

      Element
        :childrenSize
        :fitParent
        if portrait then :column else :row
        padding: 10
        childrenMargins: 10

        Element
          :childrenSize
          GameStyles.boardSection

          &Board {}
            endNumber: 110
            @setHoverNumber, @clearHoverNumber
            @hoverNumber

        Element
          secondaryBoardAreaLayout = if portrait
            :parentWidthChildrenHeight
          else
            :childrenSize
          :column
          childrenMargins: 10

          GameHeader()

          Element
            secondaryBoardAreaLayout
            :row
            childrenMargins: 10

            array i from 0 to 1
              pawnId = players[currentPlayerId].pawnIds[i]
              Element
                secondaryBoardAreaLayout
                :row
                :childrenCenterCenter
                GameStyles.boardSection
                &PawnTurnOptions {} pawnId

        if winner
          Element
            inFlow: false
            draw: #fff7
            on: pointerClick: @models.currentGame.resetGame
            :pointerCursor
            Element
              :childrenSize
              padding: 20
              :centerCenter
              :row
              GameStyles.overlay

              TextElement
                :childrenSize
                text: @models.currentGame.players[winner].emoji
                fontFamily: :arial
                fontSize: 100

              TextElement
                :childrenSize
                text: " is the WINNER!"
                fontFamily: :arial
                fontSize: 100
